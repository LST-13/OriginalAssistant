plugins {
    id 'com.android.application'
    id 'stringfog'
}
import com.github.megatronking.stringfog.plugin.kg.RandomKeyGenerator

stringfog {
    enable true
    debug false
    implementation 'com.github.megatronking.stringfog.xor.StringFogImpl'
    fogPackages = ["fun.qianxiao.originalassistant"]
    kg new RandomKeyGenerator()
    mode base64
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "fun.qianxiao.originalassistant"
        minSdk 23
        targetSdk 32
        versionCode 5
        versionName "1.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def outputFile = output.outputFile
                def fileName
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    if (variant.buildType.name == 'release') {
                        fileName = "OriginalAssistant.v.${defaultConfig.versionName}.apk"
                    } else if (variant.buildType.name == 'debug') {
                        fileName = "OriginalAssistant.v.${defaultConfig.versionName}_debug.apk"
                    }
                    outputFileName = fileName
                }
            }
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference:1.1.1'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // 工具库 https://github.com/Blankj/AndroidUtilCode/blob/master/lib/utilcode/README-CN.md
    implementation 'com.blankj:utilcodex:1.31.0'

    // Logger https://github.com/orhanobut/logger
    implementation 'com.orhanobut:logger:2.2.0'

    // 弹窗XPopup https://github.com/li-xiaojun/XPopup/wiki/
    implementation 'com.github.li-xiaojun:XPopup:2.7.2'

    // 网络请求okhttp3 https://github.com/square/okhttp
    implementation "com.squareup.okhttp3:okhttp:4.9.3"

    // EasyHttp
    implementation 'com.github.getActivity:EasyHttp:11.8'

    // Retrofit https://github.com/square/retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    // 反序列化json工具
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'

    // RxJava https://github.com/ReactiveX/RxJava
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    // RxAndroid https://github.com/ReactiveX/RxAndroid
    implementation "io.reactivex.rxjava3:rxjava:3.1.3"

    // 图片加载Glide https://github.com/bumptech/glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.12.0'

    // BigImageViewPager https://github.com/SherlockGougou/BigImageViewPager
    implementation 'com.github.SherlockGougou:BigImageViewPager:androidx-7.2.3'

    // FlycoTabLayout https://github.com/H07000223/FlycoTabLayout
    implementation 'io.github.h07000223:flycoTabLayout:3.0.0'

    // FloatingActionMenu https://github.com/zendesk/android-floating-action-button
    implementation 'com.getbase:floatingactionbutton:1.10.1'

    // sqlite utils https://github.com/pardom-zz/ActiveAndroid
    //implementation 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'

    // 友盟统计SDK https://mobile.umeng.com/platform/apps/list
    implementation  'com.umeng.umsdk:common:9.5.0'// 必选
    implementation  'com.umeng.umsdk:asms:1.4.1'// 必选

    // xor加解密库，stringfog中的implementation需依赖此库 https://github.com/MegatronKing/StringFog-Sample1
    implementation 'com.github.megatronking.stringfog:xor:4.0.1'

    // commons-text utils. Such as StringEscapeUtils etc. https://github.com/apache/commons-text
    implementation 'org.apache.commons:commons-text:1.10.0'

    // XBanner https://github.com/xiaohaibin/XBanner
    implementation 'com.github.xiaohaibin:XBanner:androidx_v1.2.6'
}